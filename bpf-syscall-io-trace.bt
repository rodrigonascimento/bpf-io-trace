#!/usr/bin/env bpftrace 

/*
 * Desc: bpftrace script to capture info about IO syscalls
 * Author: Rodrigo Nascimento
 */

BEGIN
{
    @ptrace = str($1);
    @app_dir = str($2);
    printf("start_at=%s\n", strftime("%s.%f", nsecs));
}

tracepoint:syscalls:sys_enter_open,
tracepoint:syscalls:sys_enter_openat,
tracepoint:syscalls:sys_enter_openat2
/comm == @ptrace/
{
    if (strcontains(str(args->filename),@app_dir)) {
        @filename[tid] = args->filename;
    }
}

tracepoint:syscalls:sys_enter_creat
/comm == @ptrace/
{
    if (strcontains(str(args->filename),@app_dir)) {
        @filename[tid] = args->pathname;
    }
}

tracepoint:syscalls:sys_exit_open*,
tracepoint:syscalls:sys_exit_creat
/ @filename[tid] /
{
    $ret = args->ret;
    $fd = $ret >= 0 ? $ret : -1;
    if ( $fd >= 0 ) {
        @fdfile[tid,$fd] = @filename[tid];
        @fdfilename[tid,$fd] = str(@filename[tid]);
        printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld\n", strftime("%s.%f", nsecs), args->__syscall_nr, 
        probe, comm, pid, tid, str(@fdfile[tid,$fd]), $fd);
    }
}

tracepoint:syscalls:sys_enter_read
/ @fdfile[tid,args->fd] /
{
    @op_read_fd[tid] = args->fd;
    @op_read_req_size_b[tid,@op_read_fd[tid]] = args->count;
    @op_read_lat[tid,@op_read_fd[tid]] = nsecs
}

tracepoint:syscalls:sys_exit_read
/ @fdfile[tid,@op_read_fd[tid]] /
{
    $ret = args->ret;
    $bytes_read = $ret >=0 ? $ret : -1;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld req_size_bytes=%ld bytes_read=%ld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_read_fd[tid]], @op_read_fd[tid], 
        (nsecs - @op_read_lat[tid,@op_read_fd[tid]]), @op_read_req_size_b[tid,@op_read_fd[tid]], $bytes_read);

    delete(@op_read_req_size_b[tid,@op_read_fd[tid]]);
    delete(@op_read_lat[tid,@op_read_fd[tid]]);
    delete(@op_read_fd[tid]);
}

tracepoint:syscalls:sys_enter_pread64
/ @fdfile[tid,args->fd] /
{
    @op_pread64_fd[tid] = args->fd;
    @op_pread64_req_size_b[tid,@op_pread64_fd[tid]] = args->count;
    @op_pread64_offset[tid,@op_pread64_fd[tid]] = args->pos;
    @op_pread64_lat[tid,@op_pread64_fd[tid]] = nsecs;
}

tracepoint:syscalls:sys_exit_pread64
/ @fdfile[tid,@op_pread64_fd[tid]] /
{
    $ret = args->ret;
    $bytes_read = $ret >=0 ? $ret : -1;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld req_size_bytes=%ld offset=%ld bytes_read=%ld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_pread64_fd[tid]], @op_pread64_fd[tid], 
        (nsecs - @op_pread64_lat[tid,@op_pread64_fd[tid]]), @op_pread64_req_size_b[tid,@op_pread64_fd[tid]], @op_pread64_offset[tid,@op_pread64_fd[tid]], $bytes_read);

    delete(@op_pread64_req_size_b[tid,@op_pread64_fd[tid]]);
    delete(@op_pread64_offset[tid,@op_pread64_fd[tid]]);
    delete(@op_pread64_lat[tid,@op_pread64_fd[tid]]);
    delete(@op_pread64_fd[tid]);
}

tracepoint:syscalls:sys_enter_preadv
/ @fdfile[tid,args->fd] /
{
    @op_preadv_fd[tid] = args->fd;
    @op_preadv_lat[tid,@op_preadv_fd[tid]] = nsecs;
    @op_preadv_offset_l[tid,@op_preadv_fd[tid]] = args->pos_l;
    @op_preadv_offset_h[tid,@op_preadv_fd[tid]] = args->pos_h;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld offset_l=%ld offset_h=%ld ", strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, 
    pid, tid, @fdfilename[tid,@op_preadv_fd[tid]], @op_preadv_fd[tid], @op_preadv_offset_l[tid,@op_preadv_fd[tid]], @op_preadv_offset_h[tid,@op_preadv_fd[tid]]);
}

tracepoint:syscalls:sys_exit_preadv
/ @fdfile[tid,@op_readv_fd[tid]] /
{
    $ret = args->ret;
    $bytes_read = $ret >=0 ? $ret : -1;
    printf("lat=%lld bytes_read=%ld\n", (nsecs - @op_readv_lat[tid,@op_preadv_fd[tid]]), $bytes_read);

    delete(@op_preadv_fd[tid]);
    delete(@op_preadv_lat[tid,@op_preadv_fd[tid]]);
    delete(@op_preadv_offset_l[tid,@op_preadv_fd[tid]]);
    delete(@op_preadv_offset_h[tid,@op_preadv_fd[tid]]);
}

tracepoint:syscalls:sys_enter_readv
/ @fdfile[tid,args->fd] /
{
    @op_readv_fd[tid] = args->fd;
    @op_readv_lat[tid,@op_readv_fd[tid]] = nsecs;
    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld ", strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, 
    pid, tid, @fdfilename[tid,@op_readv_fd[tid]], @op_readv_fd[tid]);
}

tracepoint:syscalls:sys_exit_readv
/ @fdfile[tid,@op_readv_fd[tid]] /
{
    $ret = args->ret;
    $bytes_read = $ret >=0 ? $ret : -1;
    printf("lat=%lld bytes_read=%ld\n", (nsecs - @op_readv_lat[tid,@op_readv_fd[tid]]), $bytes_read);

    delete(@op_readv_fd[tid]);
    delete(@op_readv_lat[tid,@op_readv_fd[tid]]);
}

tracepoint:syscalls:sys_enter_write
/ @fdfile[tid,args->fd] /
{
    @op_write_fd[tid] = args->fd;
    @op_write_req_size_b[tid,@op_write_fd[tid]] = args->count;
    @op_write_lat[tid,@op_write_fd[tid]] = nsecs;
}

tracepoint:syscalls:sys_exit_write
/ @fdfile[tid,@op_write_fd[tid]] /
{
    $ret = args->ret;
    $bytes_written = $ret >=0 ? $ret : -1;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld req_size_bytes=%ld bytes_written=%ld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_write_fd[tid]], @op_write_fd[tid], 
        (nsecs - @op_write_lat[tid,@op_write_fd[tid]]), @op_write_req_size_b[tid,@op_write_fd[tid]], $bytes_written);

    delete(@op_write_req_size_b[tid,@op_write_fd[tid]]);
    delete(@op_write_lat[tid,@op_write_fd[tid]]);
    delete(@op_write_fd[tid]);
}

tracepoint:syscalls:sys_enter_pwrite64
/ @fdfile[tid,args->fd] /
{
    @op_pwrite64_fd[tid] = args->fd;
    @op_pwrite64_req_size_b[tid,@op_pwrite64_fd[tid]] = args->count;
    @op_pwrite64_offset[tid,@op_pwrite64_fd[tid]] = args->pos;
    @op_pwrite64_lat[tid,@op_pwrite64_fd[tid]] = nsecs;
}

tracepoint:syscalls:sys_exit_pwrite64
/ @fdfile[tid,@op_pwrite64_fd[tid]] /
{
    $ret = args->ret;
    $bytes_written = $ret >=0 ? $ret : -1;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld req_size_bytes=%ld offset=%ld bytes_written=%ld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_pwrite64_fd[tid]], @op_pwrite64_fd[tid], 
        (nsecs - @op_pwrite64_lat[tid,@op_pwrite64_fd[tid]]), @op_pwrite64_req_size_b[tid,@op_pwrite64_fd[tid]], @op_pwrite64_offset[tid,@op_pwrite64_fd[tid]], $bytes_written);

    delete(@op_pwrite64_req_size_b[tid,@op_pwrite64_fd[tid]]);
    delete(@op_pwrite64_offset[tid,@op_pwrite64_fd[tid]]);
    delete(@op_pwrite64_lat[tid,@op_pwrite64_fd[tid]]);
    delete(@op_pwrite64_fd[tid]);
}

tracepoint:syscalls:sys_enter_pwritev
/ @fdfile[tid,args->fd] /
{
    @op_pwritev_fd[tid] = args->fd;
    @op_pwritev_lat[tid,@op_pwritev_fd[tid]] = nsecs;
    @op_pwritev_offset_l[tid,@op_pwritev_fd[tid]] = args->pos_l;
    @op_pwritev_offset_h[tid,@op_pwritev_fd[tid]] = args->pos_h;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld offset_l=%ld offset_h=%ld ", strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, 
    pid, tid, @fdfilename[tid,@op_pwritev_fd[tid]], @op_pwritev_fd[tid], @op_pwritev_offset_l[tid,@op_pwritev_fd[tid]], @op_pwritev_offset_h[tid,@op_pwritev_fd[tid]]);
}

tracepoint:syscalls:sys_exit_pwritev
/ @fdfile[tid,@op_readv_fd[tid]] /
{
    $ret = args->ret;
    $bytes_written = $ret >=0 ? $ret : -1;
    printf("lat=%lld bytes_written=%ld\n", (nsecs - @op_readv_lat[tid,@op_pwritev_fd[tid]]), $bytes_written);

    delete(@op_pwritev_fd[tid]);
    delete(@op_pwritev_lat[tid,@op_pwritev_fd[tid]]);
    delete(@op_pwritev_offset_l[tid,@op_pwritev_fd[tid]]);
    delete(@op_pwritev_offset_h[tid,@op_pwritev_fd[tid]]);
}

tracepoint:syscalls:sys_enter_writev
/ @fdfile[tid,args->fd] /
{
    @op_writev_fd[tid] = args->fd;
    @op_writev_lat[tid,@op_writev_fd[tid]] = nsecs;
    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld ", strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, 
    pid, tid, @fdfilename[tid,@op_writev_fd[tid]], @op_writev_fd[tid]);
}

tracepoint:syscalls:sys_exit_writev
/ @fdfile[tid,@op_writev_fd[tid]] /
{
    $ret = args->ret;
    $bytes_written = $ret >=0 ? $ret : -1;
    printf("lat=%lld bytes_written=%ld\n", (nsecs - @op_writev_lat[tid,@op_writev_fd[tid]]), $bytes_written);
    
    delete(@op_writev_lat[tid,@op_writev_fd[tid]]);
    delete(@op_writev_fd[tid]);
}

tracepoint:syscalls:sys_enter_lseek
/ @fdfile[tid,args->fd] /
{
    @op_lseek_fd[tid] = args->fd;
    @op_lseek_offset[tid,@op_lseek_fd[tid]] = args->offset;
    @op_lseek_whence[tid,@op_lseek_fd[tid]] = args->whence;
}

tracepoint:syscalls:sys_exit_lseek
/ @fdfile[tid,@op_lseek_fd[tid]] /
{
    $ret = args->ret;
    $offset = $ret >0 ? $ret : -1;

    if ( $offset > -1) {
        printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld offset=%ld whence=%ld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_lseek_fd[tid]], @op_lseek_fd[tid], 
        $offset, @op_lseek_whence[tid,@op_lseek_fd[tid]]);
    }

    delete(@op_lseek_offset[tid,@op_lseek_fd[tid]]);
    delete(@op_lseek_whence[tid,@op_lseek_fd[tid]]);
    delete(@op_lseek_fd[tid]);
}

tracepoint:syscalls:sys_enter_unlink,
tracepoint:syscalls:sys_enter_unlinkat
/ @filename[tid] /
{
    @op_unlink_filename[tid] = args->pathname;
    @op_unlink_lat[tid,@op_unlink_filename[tid]] = nsecs;
    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s ", 
    strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, str(args->pathname));
}

tracepoint:syscalls:sys_exit_unlink*
/ @op_unlink_filename[tid] /
{
    $ret = args->ret;
    if ( $ret > -1 ) {
        printf("lat=%lld\n", (nsecs - @op_unlink_lat[tid,@op_unlink_filename[tid]]));
    }

    delete(@op_unlink_filename[tid]);
    delete(@op_unlink_lat[tid,@op_unlink_filename[tid]]);
} 

tracepoint:syscalls:sys_enter_fdatasync
/ @fdfile[tid,args->fd] /
{
    @op_fdatasync_fd[tid] = args->fd;
    @op_fdatasync_lat[tid,@op_fdatasync_fd[tid]] = nsecs;
}

tracepoint:syscalls:sys_exit_fdatasync
/ @fdfile[tid,@op_fdatasync_fd[tid]] /
{
    $ret = args->ret;

    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_fdatasync_fd[tid]], @op_fdatasync_fd[tid], 
        (nsecs - @op_fdatasync_lat[tid,@op_fdatasync_fd[tid]]));

    delete(@op_fdatasync_lat[tid,@op_fdatasync_fd[tid]]);
    delete(@op_fdatasync_fd[tid]);
}

tracepoint:syscalls:sys_enter_fsync
/ @fdfile[tid,args->fd] /
{
    @op_fsync_fd[tid] = args->fd;
    @op_fsync_lat[tid, @op_fsync_fd[tid]] = nsecs;
}

tracepoint:syscalls:sys_exit_fsync
/ @fdfile[tid,@op_fsync_fd[tid]] /
{
    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld lat=%lld\n",
        strftime("%s.%f", nsecs), args->__syscall_nr, probe, comm, pid, tid, @fdfilename[tid,@op_fsync_fd[tid]], @op_fsync_fd[tid], 
        (nsecs - @op_fsync_lat[tid,@op_fsync_fd[tid]]));

    delete(@op_fsync_lat[tid,@op_fsync_fd[tid]]);
    delete(@op_fsync_fd[tid]);
}

tracepoint:syscalls:sys_enter_close
/ @fdfile[tid,args->fd] /
{
    printf("time=%s syscall=%d probe=%s process=%s pid=%d tid=%d filename=%s fd=%ld\n", strftime("%s.%f", nsecs), args->__syscall_nr, 
    probe, comm, pid, tid, @fdfilename[tid,args->fd], args->fd);
    delete(@fdfilename[tid,args->fd]);
    delete(@fdfile[tid,args->fd]);
    delete(@filename[tid]);
}

END
{
    printf("finished_at=%s\n", strftime("%s.%f", nsecs));
    clear(@ptrace);
    clear(@app_dir);
    clear(@filename);
    clear(@fdfile);
    clear(@fdfilename);
    clear(@op_read_fd);
    clear(@op_read_req_size_b);
    clear(@op_pread64_fd);
    clear(@op_pread64_req_size_b);
    clear(@op_pread64_offset);
    clear(@op_write_fd);
    clear(@op_write_req_size_b);
    clear(@op_pwrite64_fd);
    clear(@op_pwrite64_req_size_b);
    clear(@op_pwrite64_offset);
    clear(@op_writev_fd);
    clear(@op_lseek_fd);
    clear(@op_lseek_offset);
    clear(@op_lseek_whence);
    clear(@op_read_lat);
    clear(@op_pread64_lat);
    clear(@op_write_lat);
    clear(@op_pwrite64_lat);
    clear(@op_writev_lat);
    clear(@op_fdatasync_fd);
    clear(@op_fdatasync_lat);
    clear(@op_fsync_fd);
    clear(@op_fsync_lat);
    clear(@op_readv_fd);
    clear(@op_readv_lat);
    clear(@op_preadv_fd);
    clear(@op_preadv_lat);
    clear(@op_preadv_offset_l);
    clear(@op_preadv_offset_h);
    clear(@op_pwritev_fd);
    clear(@op_pwritev_lat);
    clear(@op_pwritev_offset_l);
    clear(@op_pwritev_offset_h);
}
